%type int = {int}
%type str = {byte[:]}
%func run

%tok id = /[a-zA-Z_][a-zA-Z0-9_]+/
%tok num = /[0-9_]+/
%skip = " "

%start calc

calc<int>
	: asnexpr {-> a0}
	| calc asnexpr {-> a0}
	;

asnexpr<int>
	: id "=" expr {
			std.htput(dict, a0, a2)
                        -> a2
		}
	| expr {
                        -> a0
		}
		
	;

expr<int>
	: term {-> a0}
	| expr "+" term {-> a0 + a2}
	| expr "-" term {-> a0 - a2}
	;

term<int>
	: atomic	{ -> a0}
	| term "*" atomic	{ -> a0 * a2}
	| term "/" atomic	{ -> a0 / a2}
	| term "%" atomic	{ -> a0 % a2}
	;

atomic<int>
	: id {-> std.htgetv(dict, a0, 0)}
	| num {-> std.get(std.intparse(a0))}
	| "(" expr ")" {-> a1}
    	;

%myr {
use std
use bio

var dict

const main = {
	var f

	f = bio.mkfile(0, bio.Rd)
	dict = std.mkht(std.strhash, std.streq)
        while true
		std.put("demo> ")
		match bio.readln(f)
		| `bio.Ok ln:
			if std.sleq(ln, "quit")
				break
			;;
                        match run(ln)
                        | `std.Some r:  std.put("{}\n", r)
                        | `std.None:    std.put("Failed to parse input {}\n", ln)
			;;
			std.slfree(ln)
		| `bio.Err e:   std.put("error reading line: {}\n", e)
                | `bio.Eof:
		;;
	;;
}
}

