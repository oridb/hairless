use std

use "types.use"

pkg pg =
	const parse	: (f : byte[:] -> rules#)
;;


type parser = struct
	/* parse input */
	data	: byte[:]
	rest	: byte[:]
	fname	: byte[:]
	line	: int

	start	: byte[:]
	nextsym	: sym
	symid	: std.htab(byte[:], sym)#
	symname	: std.htab(sym, byte[:])#
	isterm	: std.bitset#
;;

const parse = {f
	var rl, p

	p = mkparser(f)
	rl = std.mk([
		.infile = f,
		.isterm = p.isterm,
		.symname = p.symname,
		.types = std.mkht(std.strhash, std.streq),
		.stypes = std.mkht(std.inthash, std.inteq)
	])

	std.htput(rl.types, "_str", "byte[:]")

	/* we always have Eof token */
	std.bsput(p.isterm, symid(p, "Eof"))
	while !iseof(p)
		toplev(p, rl)
	;;

	rl.nsym = p.nextsym
	if rl.start == TokEof
		std.fatal("missing %start <start symbol>\n")
	;;
	if rl.func.len == 0
		rl.func = "parse"
	;;
	-> rl
}

const mkparser = {path
	var p

	p = std.mk([
		.line = 1,
		.fname = std.sldup(path),
		.symid = std.mkht(std.strhash, std.streq),
		.symname = std.mkht(std.inthash, std.inteq),
		.isterm = std.mkbs(),
	])
	match std.slurp(path)
	| `std.Ok d:	p.data = d
	| `std.Err _:	std.fatal("could not open '{}'\n", path)
	;;
	p.rest = p.data
	-> p
}

$noret const failparse = {p : parser#, msg, args : ...
	var sb, str, ap

	ap = std.vastart(&args)
	sb = std.mksb()
	std.sbfmt(sb, "{}:{}: ", p.fname, p.line)
	std.sbfmtv(sb, msg, &ap)

	str = std.sbfin(sb)
	std.put("{}", str)
	std.slfree(str)
	std.exit(1)
}

const toplev = {p, rl
	skipspace(p)
	if matchc(p, '%')
		directive(p, rl)
	else
		rule(p, rl)
	;;
}

const directive = {p, rl
	var u
	var s

	match word(p)
	| `std.Some "start":
		if rl.start != TokEof
			failparse(p, "'start' already sepecified\n")
		;;
		s = expect(p, word(p), "expected start symbol name after 'start'\n")
		rl.start = symid(p, s)
	| `std.Some "pkg":
		checkunset(p, rl.package, "'pkg' already sepecified\n")
		rl.package = expect(p, word(p), "expected package name after 'pkg'\n")
	| `std.Some "use":
		u = expect(p, usename(p), "expected use name after 'pkg'\n")
		std.slpush(&rl.uses, u)
	| `std.Some "myr":
		checkunset(p, rl.myr, "'pkg' already sepecified\n")
		rl.myr = expect(p, myrlit(p), "expected myrddin literal after 'myr'\n")
	| `std.Some "tok":
		token(p, rl, false)
	| `std.Some "skip":
		token(p, rl, true)
	| `std.Some "type":
		rtype(p, rl)
	| `std.Some "func":
		checkunset(p, rl.func, "already set %func")
		rl.func = expect(p, word(p), "expected function name after %func")
	| `std.Some d:
		failparse(p, "unknown directive {}\n", d)
	| `std.None:
		failparse(p, "expected word after '%'\n")
	;;
}

const rtype = {p, rl
	var name, ty

	name = expect(p, word(p), "expected token name after '%type'\n")
	skipspace(p)
	if !matchc(p, '=')
		failparse(p, "expected '=' after type name\n")
	;;
	ty = expect(p, myrlit(p), "expected myrddin literal after %type\n")
	addtype(rl, name, ty)
}

const addtype = {rl, name, ty
	std.htput(rl.types, name, ty)
}

const token = {p, rl, skip
	var name, pat
	var isregex

	name = ""
	if skip
		name = std.fmt(".itok{}", rl.toks.len)
	else
		name = expect(p, word(p), "expected token name after '%tok'\n")
	;;
	skipspace(p)
	if !matchc(p, '=')
		failparse(p, "expected '=' after token name\n")
	;;

	skipspace(p)
	match peekc(p)
	| '/':
		isregex = true
		pat = expect(p, quoted(p, '/'), "expected token regex\n")
	| '"':
		isregex = false
		pat = expect(p, quoted(p, '"'), "expected token regex\n")
	| c:
		failparse(p, "invalid token pattern starting with '{}'\n", c)
	;;
	addtok(p, rl, name, isregex, pat, skip)
}

const addtok = {p, rl, name, isregex, pat, skip
	var tok : tok#

	tok = std.mk([
		.sym = symid(p, name),
		.isregex = isregex,
		.name = name,
		.pat = pat,
		.skip = skip,
	])

	std.bsput(p.isterm, tok.sym)
	std.htput(rl.stypes, tok.sym, "_str")
	std.slpush(&rl.toks, tok)
}

const rule = {p, rl
	var rtype
	var sym
	var r

	match word(p)
	| `std.None:
		/* nothing */
	| `std.Some name:
		rtype = ""
		skipspace(p)
		if matchc(p, '<')
			rtype = expect(p, word(p), "expected type in <>\n")
			skipspace(p)
			if !matchc(p, '>')
				failparse(p, "expected > after type\n")
			;;
		;;

		skipspace(p)
		if !matchc(p, ':')
			failparse(p, "expected ':' after rule name\n")
		;;
		while true
			sym = symid(p, name)
			r = rulebody(p, rl, sym)
			addrule(p, rl, r, name)
			if rtype.len > 0
				std.htput(rl.stypes, sym, rtype)
			;;

			skipspace(p)
			if matchc(p, ';')
				break
			;;
			if !matchc(p, '|')
				failparse(p, "expected '|' or ';' after rule body\n")
			;;
		;;
	;;
}

const rulebody = {p, rl, lhs -> rule#
	var rhs
	var action
	var pat

	rhs = [][:]
	while true
		skipspace(p)
		/* implicit tokens can show up here */
		if peekc(p) == '"'
			pat = expect(p, quoted(p, '"'), "expected token regex\n")
			addtok(p, rl, pat, false, pat, false)
			std.slpush(&rhs, symid(p, pat))
		else
			match word(p)
			| `std.Some w:	std.slpush(&rhs, symid(p, w))
			| `std.None:	break
			;;
		;;
	;;
	skipspace(p)
	if peekc(p) == '{'
		match myrlit(p)
		| `std.None:	failparse(p, "expected myrlit\n")
		| `std.Some lit:
			action = lit
		;;
	else
		action = ""
	;;
	-> std.mk([.lhs=lhs, .rhs=rhs, .act=action])
}

const symid = {p, name
	match std.htget(p.symid, name)
	| `std.Some id:
		-> id
	| `std.None:
		std.htput(p.symid, name, p.nextsym)
		std.htput(p.symname, p.nextsym, name)
		-> p.nextsym++
	;;
}

generic expect = {p, tok : std.option(@a), msg -> @a
	match tok
	| `std.Some w:	-> w
	| `std.None:	failparse(p, msg)
	;;
}

const usename = {p
	skipspace(p)
	if peekc(p) == '"'
		-> quoted(p, '"')
	else
		-> word(p)
	;;
}

const quoted = {p, quot
	var start
	var n, chr

	n = 0
	skipspace(p)
	if !matchc(p, quot)
		-> `std.None
	;;
	start = p.rest
	while p.rest.len > 0
		chr = getc(p)
		match chr
		| '\n':	break
		| '\\':
			n += std.charlen(getc(p))
		| c:
			if c == quot
				goto foundquote
			;;
		;;
		n += std.charlen(chr)
	;;
	failparse(p, "missing closing quote in string")
:foundquote
	-> `std.Some std.sldup(start[:n])
}


const word = {p
	var c, n
	var start

	n = 0
	skipspace(p)
	start = p.rest
	while p.rest.len > 0
		c = peekc(p)
		if wordchar(c)
			getc(p)
			n += std.charlen(c)
		else
			break
		;;
	;;
	if n > 0
		-> `std.Some std.sldup(start[:n])
	else
		-> `std.None
	;;
}

const wordchar = {c
	-> std.isalnum(c) || c == '_' || c == '$'
}

const myrlit = {p
	var inquote, instr
	var depth
	var sb

	skipspace(p)
	if !matchc(p, '{')
		-> `std.None
	;;
	sb = std.mksb()
	depth = 1
	inquote = false
	instr = false
	while !iseof(p)
		match getc(p)
		| '\\':	
			std.sbputc(sb, '\\')
			std.sbputc(sb, getc(p))
		| '"':	
			std.sbputc(sb, '"')
			instr = !instr
		| '\'':	
			std.sbputc(sb, '\'')
			inquote = !inquote
		| '{':
			if depth > 0
				std.sbputc(sb, '{')
			;;
			if !inquote && !instr
				depth++
			;;
		| '}':
			if !inquote && !instr
				depth--
				if depth == 0
					goto foundmyr
				;;
			;;
			std.sbputc(sb, '}')
		| c:
			std.sbputc(sb, c)
		;;
	;;
	failparse(p, "missing closing '}' in myrddin literal\n")
:foundmyr
	-> `std.Some std.sbfin(sb)
}

const skipspace = {p
	var c, r

	r = p.rest
	while r.len > 0
		c = peekc(p)
		match c
		| ' ':	getc(p)
		| '\t':	getc(p)
		| '\n':
			getc(p)
			p.line++
		| '#':
			while p.rest.len > 0 && peekc(p) != '\n'
				getc(p)
			;;
		| _:
			break
		;;
	;;
}

const matchc = {p, c
	var chr, s

	if p.rest.len == 0
		-> false
	;;
	(chr, s) = std.strstep(p.rest)
	if c == chr
		p.rest = s
		-> true
	else
		-> false
	;;
}

const peekc = {p
	-> std.decode(p.rest)
}

const getc = {p
	var c, s

	(c, s) = std.strstep(p.rest)
	p.rest = s
	-> c
}

const iseof = {p
	-> p.rest.len == 0
}

const addrule = {p, rl, r, name
	r.id = rl.rules.len + 1
	std.slpush(&rl.rules, r)
}

const checkunset = {p, s, msg
	if s.len > 0
		failparse(p, msg)
	;;
}
